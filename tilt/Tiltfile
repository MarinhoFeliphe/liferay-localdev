# global vars
repo=os.environ.get('LOCALDEV_REPO', '/repo')

# Function process_extension
def process_extension(
    name, projectPath, workload, targetPort, virtual_instance_id='dxp.localdev.me',
    source_deps=[], objects=[], port_forwards=[], resource_deps=[], links=[], cpu='', memory=''):

  more_deps = []
  for source_dep in source_deps:
    more_deps.append("/workspace/%s/%s" % (projectPath, source_dep))

  gradlePath=projectPath.replace('/', ':')

  build_args=" ".join([
    "/workspace/gradlew",
    "--project-dir",
    "/workspace",
    ":%s:clean" % gradlePath,
    ":%s:buildClientExtensionDockerImage" % gradlePath,
    "--stacktrace",
    "-PimageId=$EXPECTED_REF"])

  custom_build(
    name,
    build_args,
    deps=[
      "/workspace/%s/src" % projectPath,
      "/workspace/%s/build.gradle" % projectPath,
      "/workspace/%s/client-extension.yaml" % projectPath,
      "/workspace/%s/Dockerfile" % projectPath,
    ] + more_deps,
    ignore=[])

  assemble_args=" ".join([
    "/workspace/gradlew",
    "--project-dir",
    "/workspace",
    ":%s:clean" % gradlePath,
    ":%s:assembleClientExtension" % gradlePath,
    "--stacktrace",
  ])

  local(assemble_args)

  config_json_file = "/workspace/%s/build/clientExtension/%s.client-extension-config.json" % (projectPath, name)

  init_metadata = False

  if workload != "static":
    init_metadata = True

  ytt_args=[
    'ytt',
    "-f %s/k8s/workloads/%s" % (repo, workload),
    "-f %s" % config_json_file, 
    "--data-value-yaml initMetadata=%s" % init_metadata,
    "--data-value image=%s" % name,
    "--data-value serviceId=%s" % name,
    "--data-value-yaml targetPort=%s" % targetPort,
    "--data-value virtualInstanceId=%s" % virtual_instance_id]

  if cpu != '':
    ytt_args.append("--data-value-yaml cpu=%s" % cpu)
  
  if memory != '':
    ytt_args.append("--data-value-yaml memory=%s" % memory)

  k8s_yaml(local(" ".join(ytt_args)))

  if workload != 'job':
    objects=[
      "%s:ingress" % name, 
      "%s:ingressroute" % name]

  k8s_resource(
    labels=['dxp-client-extensions'],
    port_forwards=port_forwards,
    objects=['%s-%s-lxc-ext-provision-metadata:configmap' % (name, virtual_instance_id)] + objects,
    resource_deps=['dxp.localdev.me'] + resource_deps,
    workload=name,
    links=links)

#main code

update_settings(max_parallel_updates=1)

watch_file("%s/k8s/endpoint/" % repo)
watch_file("%s/k8s/workloads/" % repo)

if config.tilt_subcommand == 'down':
  local('kubectl delete cm -l lxc.liferay.com/metadataType=ext-init')
  local("%s/scripts/dxp-stop.sh" % repo)

# build and launch dxp

local_resource(
  'dxp.localdev.me',
  cmd="",
  serve_cmd="%s/scripts/dxp-restart.sh" % repo,
  deps=["%s/docker/images/dxp-server" % repo],
  readiness_probe=probe(
      initial_delay_secs=120,
      timeout_secs=5,
      period_secs=5,
      failure_threshold=99,
      exec=exec_action(["%s/scripts/dxp-status.sh" % repo])),
  trigger_mode = TRIGGER_MODE_MANUAL,
  links=[
    link('https://dxp.localdev.me'),
    link('https://dxp.localdev.me/o/api', 'Liferay API Explorer'),
  ],
  labels=['dxp'])

for client_extension_yaml_file in str(local("find /workspace/client-extensions -name client-extension.yaml -not -path '*/build/*' -not -path '*/node_modules/*' -not -path '*/node_modules_cache/*' 2>/dev/null")).splitlines():
  project_path=os.path.dirname(client_extension_yaml_file)
  client_extension_name=os.path.basename(project_path)
  client_extension_object=read_yaml(client_extension_yaml_file)

  # defaults
  workload='static'
  resource_deps=[]
  targetPort=80
  watch=[]
  cpu=''
  memory=''

  if client_extension_object.get('runtime'):
    if client_extension_object['runtime'].get('workload') != None:
      workload=client_extension_object['runtime']['workload']

    if client_extension_object['runtime'].get('deps') != None:
      resource_deps=[client_extension_object['runtime']['deps'][0]]

    if client_extension_object['runtime'].get('port') != None:
      targetPort=client_extension_object['runtime']['port']

    if client_extension_object['runtime'].get('watch') != None:
      watch=client_extension_object['runtime']['watch']

    if client_extension_object['runtime'].get('cpu') != None:
      cpu=client_extension_object['runtime']['cpu']

    if client_extension_object['runtime'].get('memory') != None:
      memory=client_extension_object['runtime']['memory']

  # TODO call a script that can expand globs that may be passed into watch property
  # paths=local(/repo/scripts/unroll-globs.sh watch)
  # source_deps=paths

  process_extension(
    client_extension_name,
    "client-extensions/%s" % client_extension_name,
    source_deps=watch,
    targetPort=targetPort,
    workload=workload,
    cpu=cpu,
    memory=memory,
    resource_deps=resource_deps)